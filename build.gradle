/*
 * Copyright (c) 2015 eSailors IT Solutions GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'nexus'
apply plugin: 'release'

group = 'de.esailors.gradle'

// Specify that during a release the artifacts are automatically uploaded to Nexus
createReleaseTag.dependsOn uploadArchives
release.dependsOn clean
project.ext.set("gradle.release.useAutomaticVersion", "true")

project.ext {
   gradleVersion = '1.10'
   checkstyleConfigDir  = new File(rootDir, 'config/checkstyle')
   ignoreTestFailures = false
}
// Maven release credentials are only specified in the gradle.properties file on Jenkins
// Don't fail, if these properties are not available
if (!project.properties.containsKey("services_os_release_user")) {
  project.ext.services_os_release_user = "";
  project.ext.services_os_release_password = "";
}


buildscript {
    repositories {
        maven {
            url "http://codebase.office.tipp24.de:8081/nexus/content/groups/argo-dynamic"
        }
        jcenter()

    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:+'
	classpath 'com.github.townsfolk:gradle-release:1.2.2-esailors'
    }
}

repositories {
    maven {
        url "http://codebase.office.tipp24.de:8081/nexus/content/groups/argo-dynamic"
    }
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Canary Build plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

modifyPom {
    project {
        name 'Gradle Canary Build plugin'
        description 'Gradle Canary Build plugin that provides a task to create project builds with latest dependency versions.'
        inceptionYear '2015'
        developers {
            developer {
                id 'sjaco'
                name 'Steffen Jacobs'
                email 'steffen.jacobs@gmail.com'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
